
function getDEGBoxPlots
(
	CSV degs,
	Matrix exprMatrix,
	CSV sampleGroupsSimple,
	CSV geneAnnot,
	string direction,
	record group,
	string sectionType,
	string groupOrder = "",
	boolean labelOutliers = true,
	float cexGroupLabel = 1,
	float height = 7
) -> (Latex report)
{
	// which direction?
	if (direction == "up") {
		degsFiltered = TableQuery(table1 = degs, query='SELECT "Ensembl" FROM table1 WHERE "fc" > 0 LIMIT 28')
		caption = 'Top-28 genes with higher expression in '+group.caseG+" than "+group.controlG+" samples. Genes ordered by statistical significance (qValue) from top-left to bottom-right."
		sectionTitle = 'Top-28 significant genes with higher expression in '+group.caseG+' than '+group.controlG+' samples'                         
	} 
	else if (direction == "down") {
		degsFiltered = TableQuery(table1 = degs, query='SELECT "Ensembl" FROM table1 WHERE "fc" < 0 LIMIT 28')                        
		caption = 'Top-28 genes with lower expression in '+group.caseG+" than "+group.controlG+" samples. Genes ordered by statistical significance (qValue) from top-left to bottom-right."                         
		sectionTitle = 'Top-28 significant genes with lower expression in '+group.caseG+' than '+group.controlG+' samples'                         
	}
	else {
		std.fail("Invalid direction: "+direction)
	}

	geneBoxplot = ExpressionBoxPlot
	(
  		expr   = exprMatrix,
		groups = sampleGroupsSimple,
		groupOrder = groupOrder,
		annotation = geneAnnot,
		geneIds = degsFiltered,
		height = height,
		nRow = 4,
		nCol = 7,
		labelOutliers=labelOutliers,
		cexSampleLabel=0.3,
		cexGroupLabel=cexGroupLabel,
		caption = caption,
		sectionTitle=sectionTitle, 
		sectionType=sectionType
	)

	return geneBoxplot.document
}