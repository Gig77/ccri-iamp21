// ----------------- //
//  QUALITY CONTROL  //
// ----------------- //

/** Prepares some quality control plots for the given expression matrix. */
function getQCReport(Array<BAM>	 alignedBAMs,
				GTF              gtf,
				Matrix           expr,
                SampleGroupTable groups,
                string           title = "Quality Control") ->
               (Latex            report) {

	readStats = RNASeqReadStats(reads=alignedBAMs, gtf=gtf)
	readStatsChart = RNASeqReadStatsBarChart(stats=readStats.counts)

    cumsum = Plot2D(y              = expr,
                    sectionTitle   = "Distribution of the expression values",
                    caption        = "Ordered list of expression values",
                    legendPosition = "off",
                    xLabel         = "genes",
                    yLabel         = "expression level",
                    title          = "",
                    sort           = true,
                    plotType       = "l",
                    imageType      = "single",
                    height         = 11)

    hclust = ClusterReport(matr         = expr,
                           showDistance = false,
                           sectionTitle = "Hierarchical clustering of the samples")

    boxplot = BoxPlot(matr1      = expr,
                      groups     = groups,
                      caption1   = "",
                      height     = 5,
                      width      = 9,
                      pagebreak  = true,
                      drawLegend = false,
                      plotType   = "boxplot",
                      pngImage   = true,
                      title1     = "Distributions of the expression values per sample",
                      @bind      = cumsum)

    mds2D = MDSPlot(expr         = expr,
                    groups       = groups,
                    sectionTitle = "Two dimensional sample clustering",
                    dimensions   = 2,
                    plotNames    = true,
                    width        = 10,
                    pagebreak    = true,
                    caption       = "Multidimensional scaling. Sample distances in the plot are proportional to differences in global gene expression.",
                    @bind        = boxplot)

    report = LatexCombiner(readStatsChart.document,
    					   cumsum,
                           boxplot,
                           hclust.report,
                           mds2D.report,
                           sectionTitle = title,
                           sectionType  = "section",
                           pagebreak    = true)
    return report
}

