function experimentalSetupReport
(
	SampleGroupTable sampleGroups,
	SampleGroupTable comparisons
) -> (Latex report)
{
	samplesReport = ExperimentSetup
	(
		force groups = sampleGroups,
		definWidth   = 10.0,
		descWidth    = 6.0,
		sectionType  = "subsection",
		sectionTitle = "Sample groups"
	)
	
	comparisonsReport = ExperimentSetup
	(
		force groups = comparisons,
		definWidth   = 7.0,
		descWidth    = 9.0,
		sectionType  = "subsection",
		sectionTitle = "Comparisons between sample groups"
	)
	
	samplesGraph = GraphVisualizer
	(
		samplesReport.graph,
		reportHeight = 26,
		reportWidth  = 18,
		layout       = "spring2",
		latexTail    = '\newpage{}'
	)
	
	comparisonsGraph = GraphVisualizer
	(
		comparisonsReport.graph,
		reportHeight = 26,
		reportWidth  = 18,
		layout       = "spring2",
		latexTail    = '\newpage{}'
	)

	combinedReport = LatexCombiner
	(
		samplesReport.report,
		comparisonsReport.report,
		samplesGraph.figure,
		comparisonsGraph.figure,
		sectionTitle = "Experimental setup",
		sectionType  = "section",
		pagebreak    = true
	)
	
	return combinedReport
}
