//------------------------------------------------------------------------------------------------
//--- CONSTANTS
//------------------------------------------------------------------------------------------------

title = "iAMP21 RNA-seq"
authors = "Christian Frech"
useFunct = true

PROJECT_BASE="/mnt/projects/iamp"
inputBamDir = INPUT(path=PROJECT_BASE+"/data/bam", recursive=false)
inputBamFiles = Folder2Array(folder1 = inputBamDir, filePattern = "C57C3ACXX_CV_([^_]+)_.*[.]bam$")
sampleAnnotation = INPUT(path=PROJECT_BASE+"/data/qlucore/annotations.txt")
comparisons = INPUT(path=PROJECT_BASE+"/data/anduril/comparisons.tsv")
gtf = INPUT(path=PROJECT_BASE+"/data/Homo_sapiens.GRCh37.75.etv6runx1.gtf.gz")

//zcat /mnt/projects/iamp/data/Homo_sapiens.GRCh37.75.etv6runx1.gtf.gz | grep 'gene_biotype "rRNA"' | perl -ne '/(ENSG[^\"]+)/; print "$1\n"' | sort | uniq > /tmp/ensemblid.rrna
//zcat /mnt/projects/iamp/data/Homo_sapiens.GRCh37.75.etv6runx1.gtf.gz | grep -vf /tmp/ensemblid.rrna | gzip -c > /mnt/projects/iamp/data/Homo_sapiens.GRCh37.75.etv6runx1.norRNA.gtf.gz
gtfNorRNA = INPUT(path=PROJECT_BASE+"/data/Homo_sapiens.GRCh37.75.etv6runx1.norRNA.gtf.gz")
geneGO = INPUT(path="/opt/moksiskaan/pipeline/exec/output/geneGO_9606.csv")  /** Gene Ontology annotations of all genes */
enrichmentTable = INPUT(path="/opt/moksiskaan/pipeline/exec/output/GOEnrichment_9606.csv") // Moksiskaan specific a priori probabilities for the Gene ontology terms.
ensemblDb = INPUT(path="/usr/local/share/anduril-bundles/moksiskaan/etc/ensemblHomoSapiens.properties") // JDBC parameters for Ensembl~\cite{Flicek2008} database.
// mysql --host=ensembldb.ensembl.org --port=5306 --database=homo_sapiens_core_73_37 --user=anonymous -B -A -e "select stable_id as ensembl,description from gene where description is not null;" > /mnt/biowaste/data2/christian.frech/iamp/results/current/anduril/ensembl.homo_sapiens_73_37.geneAnnotations.tsv
geneAnnot = INPUT(path=PROJECT_BASE+"/data/anduril/ensembl.homo_sapiens_75_37.geneAnnotations.tsv") // downloaded via Ensembl biomart http://feb2014.archive.ensembl.org/biomart/martview/b57730c5d3013cd648bcd2c966113d42
latexHeader = INPUT(path=PROJECT_BASE+"/scripts/anduril/latexHeader")
genesetCategories = INPUT(path=PROJECT_BASE+"/results/geneset_category.tsv")

fcLimitInclude  = 1.00
pLimitInclude   = 1e-5
qLimitInclude   = 1e-2
minExprInclude  = 100.00
maxNA    		= 0.90    // fraction of samples allowed to have NA values before gene is discarded

moksiskaanInit = MoksiskaanInit(showLog='EnsemblImport,PathwayCommonsImport,PINAImport,WikiPathways')
ensemblRef = XrefLinkRule(moksiskaan = moksiskaanInit.connection, columns = "Ensembl=Ensembl", xrefTypes  = XrefType_Ensembl_gene)

//------------------------------------------------------------------------------------------------
//--- SETUP SAMPLE GROUPS
//------------------------------------------------------------------------------------------------

sampleGroups = TableQuery(table1 = sampleAnnotation @require,
                          query  = """\
	SELECT * FROM (
	  SELECT "Subtype"                                     AS "ID",
		GROUP_CONCAT("Name" ORDER BY "Name" SEPARATOR ',') AS "Members",
		'median'                                           AS "Type",
		"Subtype"                                          AS "Description"
		FROM   table1
		GROUP  BY "Subtype"
      UNION
	  SELECT 'immature'                                     AS "ID",
		GROUP_CONCAT("Name" ORDER BY "Name" SEPARATOR ',') AS "Members",
		'median'                                           AS "Type",
		'Immature B-cells'          AS "Description"
		FROM   table1
		WHERE "Name" = 'S1'
      UNION
	  SELECT 'preB'                                     AS "ID",
		GROUP_CONCAT("Name" ORDER BY "Name" SEPARATOR ',') AS "Members",
		'median'                                           AS "Type",
		'preB-cells'          AS "Description"
		FROM   table1
		WHERE "Name" = 'S2'
	  UNION
	  SELECT 'mature'                                     AS "ID",
		GROUP_CONCAT("Name" ORDER BY "Name" SEPARATOR ',') AS "Members",
		'median'                                           AS "Type",
		'mature B-cells'          AS "Description"
		FROM   table1
		WHERE "Name" = 'S3'
      UNION
	  SELECT 'non-iAMP'                                     AS "ID",
		GROUP_CONCAT("Name" ORDER BY "Name" SEPARATOR ',') AS "Members",
		'median'                                           AS "Type",
		'Non-iAMP21 samples'          AS "Description"
		FROM   table1
		WHERE "Subtype" <> 'iAMP'
      UNION
	  SELECT 'non-DS'                                     AS "ID",
		GROUP_CONCAT("Name" ORDER BY "Name" SEPARATOR ',') AS "Members",
		'median'                                           AS "Type",
		'Non-Down syndrome samples'          AS "Description"
		FROM   table1
		WHERE "Subtype" <> 'DS'
      UNION
	  SELECT 'non-ER'                                     AS "ID",
		GROUP_CONCAT("Name" ORDER BY "Name" SEPARATOR ',') AS "Members",
		'median'                                           AS "Type",
		'Non-ETV6-RUNX1 samples'          AS "Description"
		FROM   table1
		WHERE "Subtype" <> 'ER'
	)	
	ORDER BY "ID"
                                   """)
sampleGroupsSimple = CSVFilter(csv=sampleGroups, regexp="ID=iAMP|DS|ER|CD19")


//------------------------------------------------------------------------------------------------
//--- SETUP COMPARISON GROUPS
//------------------------------------------------------------------------------------------------

groups = record()
for comparison : std.itercsv(comparisons) 
{
	// fetch group names
	gCase    = null
	gControl = null
	for g : std.split(comparison.Members, ',')
	{
		if (gCase    == null) { gCase    = g } else
		if (gControl == null) { gControl = g } else
		std.fail("Too many groups listed for ", comparison.ID, " (", comparison.Description, "). Members = ", comparison.Members, sep='')
	}
	
	// fetch sample names
	// make 'CD19+' match in following regular expression 
	gControlEscaped = std.strReplace(gControl,"\\+","\\\\+")
	samples = CSV2IDList
	(
		table1    = sampleGroups @require,
		columnIn  = "Members",
		columnOut = ".GeneId",
		isList    = true,
		regexp1   = "ID=("+gCase+"|"+gControlEscaped+")",
		@name     = "samples_"+comparison.ID
	)

	// add group record	
	groups[comparison.ID] = 
	{
		'caseG'    = gCase,
		'controlG' = gControl,
		'addG'     = comparison.ControlGroups,
		'samples'  = samples.ids,
		'desc'     = comparison.Description
	}
}

//------------------------------------------------------------------------------------------------
//--- EXPERIMENTAL SETUP REPORT
//------------------------------------------------------------------------------------------------

include "experimentalSetup.and"

experimentReport = experimentalSetupReport
(
	sampleGroups = sampleGroups,
	comparisons  = comparisons
)

//------------------------------------------------------------------------------------------------
//--- ALIGNMENT
//------------------------------------------------------------------------------------------------

alignedBams = record()
for bam : std.iterArray(inputBamFiles) 
{
	@out.alignment.filename = bam.key+'.gsnap.sorted.dupmarked.bam'
	gsnap = GSNAP
	(
		reads    = INPUT(path=bam.file), 
		options  = "--db=g1k_v37_etv6runx1 --dir=/data_synology/anduril/docker-images/anduril-gsnap_2014_12_28-human_g1k_v37/db/human_g1k_v37 --nthreads 15 --maxsearch=100 --npaths=1 --max-mismatches=1 --novelsplicing=0 --batch=4 --genome-unk-mismatch=0",
		docker   = "biowaste:5000/anduril/gsnap",
		@cpu     = 10, 
		@memory  = 40000,
		@name    = "gsnap_"+bam.key 
	)
	alignedBams[bam.key] = gsnap.alignment
}

//------------------------------------------------------------------------------------------------
//--- EXPRESSION MATRIX
//------------------------------------------------------------------------------------------------

//bamCounts = HTSeq(alignments = alignedBAMs, annotationGTF = gtfNorRNA, options = "-t exon -s no")
bamCounts  = {}
for sample, bam : alignedBams 
{
	sName = std.quote(sample, type="Anduril")
	@out.optOut1.filename = sName+'.htseq.counts'
	count = BashEvaluate
	(
		var1 = bam,
		var2 = gtfNorRNA,
		script = "htseq-count -f bam -t exon -s no @var1@ @var2@ | grep -v '^__' > @optOut1@",
		@name = "htseq_"+sName
	)
	bamCounts[sample] = count.optOut1
}

deseqExprMatrix = DESeqExpr
(
	geneCounts  = bamCounts,
    counts      = false,
    maxNA       = maxNA,
    normalized  = true
)

//------------------------------------------------------------------------------------------------
//--- QUALITY CONTROL
//------------------------------------------------------------------------------------------------

include "qc.and"

qcReport  = getQCReport
(
	alignedBAMs = alignedBams,
    gtf = gtf,
    counts = bamCounts,
    force expr   = deseqExprMatrix.expr,
    force groups = sampleGroupsSimple
)

//------------------------------------------------------------------------------------------------
//--- DIFFERENTIAL GENE EXPRESSION ANALYSIS
//------------------------------------------------------------------------------------------------

gMoksisA = PiispanhiippaAnnotator(sourceKeys = deseqExprMatrix.expr @require,
                                  connection = moksiskaanInit.connection,
                                  inputDB    = XrefType_Ensembl_gene,
                                  organism   = Organism_Homo_sapiens,
                                  targetDB   = "BioentityName,DNARegion")

geneNames = CSVCleaner(original   = gMoksisA.bioAnnotation,
                       columns    = "sourceKey,BioentityName,DNARegion",
                       rename     = "sourceKey=Ensembl,BioentityName=Gene",
                       skipQuotes = "*",
                       trim       = true,
                       @keep      = true)


degAllLst  = record()        // unfiltered output of DESeq2 for each comparison
degCalledLst  = record()     // only significant DEGs for each comparison
for ID, group : groups
{
	// compute differential expression statistics with DESeq2
	deseq = DESeq2
	(
		countFiles  = bamCounts,
		samples     = sampleGroups,
	    nameControl = group.controlG,
	    nameCase    = group.caseG,
	    additionalGroups = group.addG,
	    label       = ID,
	    @name       = "deseq_" + ID
	)
	degAllLst[ID] = deseq.results
	
	// annotate DESeq2 output with additional gene attributes
	deseqAnnotated = TableQuery
	(
		table1 = deseq.results @require,
		table2 = geneNames     @require,
		table3 = geneAnnot     @require,
		query  = 
			'''
			SELECT G."ids"                        AS "Ensembl",
			A."Gene"                              AS "Gene",
			D."Description"                       AS "Description",
			CONCAT(D."Chromosome Name", D."Band") AS "Band",
			G.*
			FROM table1 G
				LEFT OUTER JOIN table2 AS A ON (G."ids" = A."Ensembl")
				LEFT OUTER JOIN table3 AS D ON (G."ids" = D."Ensembl Gene ID")
			ORDER  BY "q'''+ID+'''", ABS("fc'''+ID+'''") DESC
			''',
		@name  = "deseqAnnotated_" + ID
	)
	

	// subset statistically significant DEGs
	degCalled = TableQuery
	(
		table1 = deseq @require,
		query  = 
			"""
			SELECT DISTINCT "ids"                 AS "ids", 
			                "fc"""+ID+""""        AS "fc", 
			                "meanExprE"""+ID+"""" AS "meanExprE", 
			                "meanExprC"""+ID+"""" AS "meanExprC", 
			                "p"""+ID+""""         AS "p", 
			                "q"""+ID+""""         AS "q" 
			FROM   table1
				WHERE  (ABS("fc"""+ID+"""")    >= """ + fcLimitInclude + """) AND
					   ("p"""+ID+""""          <= """ + pLimitInclude  + """) AND
					   ("q"""+ID+""""          <= """ + qLimitInclude  + """) AND
					   (("meanExprE"""+ID+"""" >= """ + minExprInclude + """) OR
					   ("meanExprC"""+ID+""""  >= """ + minExprInclude + """))
					   ORDER  BY 1
			""",
			@name  = "degCalled_"+ID
	)
	degCalledLst[ID] = degCalled.table
}

//------------------------------------------------------------------------------------------------
//--- VENN DIAGRAMS
//------------------------------------------------------------------------------------------------

degSets = CSV2SetList(tables=degCalledLst)

venn = VennDiagram
(
	sets         = degSets,
	cexSetName   = 0.3,
	cexSetSize   = 0.3,
	doWeights    = true,
	sets1        = "iAMPvsDS,iAMPvsER,ERvsDS",
	sets2        = "iAMPvsImmature,DSvsImmature,ERvsImmature",
	sets3        = "iAMPvsPreB,DSvsPreB,ERvsPreB",
	sets4        = "iAMPvsMature,DSvsMature,ERvsMature",
	sectionTitle = "Gene set comparisons",
	sectionType  = "subsection",
	types        = "circles"
)

degReportLst = record(venn=venn.report)

//------------------------------------------------------------------------------------------------
//--- DEG REPORTS
//------------------------------------------------------------------------------------------------

include "degTable.and"
include "degBoxPlot.and"
include "goClustering.and"
include "goEnrichment.and"
include "expressionHeatmap.and"

degTables = record()
for ID, group : groups 
{
	std.echo("***** Preparing results for", ID, "comparison.")
	
	//--- PREPARE TABLE WITH DIFFERENTIALLY EXPRESSED GENES IN OUTPUT FORMAT ------------------------------------------------//
	
	degTable = TableQuery
	(
		table1 = degCalledLst[ID] @require,
		table2 = geneNames        @require,
		table3 = geneAnnot        @require,
		query  = '''
			SELECT G."ids"                        AS "Ensembl",
			A."Gene"                              AS "Gene",
			G."fc"                                AS "fc",
			G."meanExprE"                         AS "exprA",
			G."meanExprC"                         AS "exprB",
			G."q"                                 AS "qValue",
			CONCAT(D."Chromosome Name", D."Band") AS "Band",
			SUBSTR(D."Description", 1, 65)        AS "Description",
			CASEWHEN(G."fc" > 0, 1, -1)           AS "status"
			FROM table1 G
				LEFT OUTER JOIN table2 AS A ON (G."ids" = A."Ensembl")
				LEFT OUTER JOIN table3 AS D ON (G."ids" = D."Ensembl Gene ID")
			ORDER  BY "qValue", ABS("fc") DESC
		''',
		@name  = "degTable_"+ID
	)

	degTables[ID] = degTable  // we write them to an Excel file later

	//--- TABLES WITH UP- AND DOWN-REULATED GENES --------------------------------------------------------------------------//

	degTableUp = getDEGTableReport
	(
		degs = degTable,
		degReportRefs = ensemblRef,
		direction = "up",
		group = group,
		ID=ID,
		section="Genes with higher expression in "+group.caseG+" than "+group.controlG+" samples",
		sectionType="subsubsection",
		@name = "degTableUp_"+ID
	)

	degTableDn = getDEGTableReport
	(
		degs = degTable,
		degReportRefs = ensemblRef,
		direction = "down",
		group = group,
		ID=ID,
		section="Genes with lower expression in "+group.caseG+" than "+group.controlG+" samples",
		sectionType="subsubsection",
		@name = "degTableDn_"+ID
	)

	//--- BOX PLOTS TOP UP- AND DOWN-REGULATED GENES ----------------------------------------------------------------------//

	degBoxplotUp = getDEGBoxPlots
	(
		degs = degTable,
		exprMatrix = deseqExprMatrix.expr,
		sampleGroupsSimple=sampleGroupsSimple,
		geneAnnot=geneAnnot,
		direction="up",
		group=group,
		sectionType="subsubsection",
		@name = "degBoxplotUp_"+ID
	)

	degBoxplotDn = getDEGBoxPlots
	(
		degs = degTable,
		exprMatrix = deseqExprMatrix.expr,
		sampleGroupsSimple=sampleGroupsSimple,
		geneAnnot=geneAnnot,
		direction="down",
		group=group,
		sectionType="subsubsection",
		@name = "degBoxplotDn_"+ID
	)

	//--- EXPRESSION HEATMAP --------------------------------------------------------------------------//

	exprMatrixFiltered = CSVFilter
	(
		csv            = deseqExprMatrix.expr,
		auxiliary      = degCalledLst[ID] @require,
		includeColumns = group.samples,
		includeColumns = "RowName",
		colOrder       = true,
		@name          = "exprMatrix_"+ID
	)                      

	exprHeatmap = getHeatmapReport
	(
		exprMatrix = exprMatrixFiltered,
		geneNames = geneNames,
		group = group,
		sectionType="subsubsection",
		@name = "heatmap"+ID
	)

	//--- GO CLUSTERING -------------------------------------------------------------------------------//

	goClustering = getGOClusteringReport
	(
		exprMatrix = exprMatrixFiltered,
		geneNames = geneNames,
		geneGO    = geneGO,
		ID = ID,
		sectionType="subsubsection",
		@name = "goClustering"+ID,
		@enabled = false
	)

	//--- GO ENRICHMENT -------------------------------------------------------------------------------//

	goEnrichment = getGOEnrichmentReport
	(
		geneIds      = degCalledLst[ID],
		geneNames    = geneNames,
		geneGO       = geneGO,
		threshold    = 0.01,
		ID           = ID,
		sectionTitle = "GO terms enriched in DEGs between " + group.caseG + " and " + group.controlG,
		sectionType  = "subsubsection",
		@name        = "goEnrichment"+ID
	)

	//--- GENE INTERACTION NETWORK --------------------------------------------------------------------//
	
	statusTable = TableQuery
	(
		table1   = degTables[ID] @require,
		table2   = degAllLst[ID] @require,
		query    = '''
			SELECT "Ensembl", "status" FROM table1
			UNION
			SELECT T2."ids" AS "Ensembl", 1 AS "status"
			FROM   table2 T2 LEFT JOIN table1 T1 ON T2."ids" = T1."Ensembl" 
			WHERE  T1."Ensembl" IS NULL AND T2."meanExpr'''+ID+'''" >= 10 AND T2."q'''+ID+'''" < 0.01 AND T2."fc'''+ID+'''" >= 1
			UNION
			SELECT T2."ids" AS "Ensembl", -1 AS "status"
			FROM   table2 T2 LEFT JOIN table1 T1 ON T2."ids" = T1."Ensembl" 
			WHERE  T1."Ensembl" IS NULL AND T2."meanExpr'''+ID+'''" >= 10 AND T2."q'''+ID+'''" < 0.01 AND T2."fc'''+ID+'''" <= -1
			UNION
			SELECT T2."ids" AS "Ensembl", 0 AS "status"
			FROM   table2 T2 LEFT JOIN table1 T1 ON T2."ids" = T1."Ensembl" 
			WHERE  T1."Ensembl" IS NULL AND T2."meanExpr'''+ID+'''" >= 300 AND T2."q'''+ID+'''" >= 0.9 AND T2."fc'''+ID+'''" > -0.1 AND T2."fc'''+ID+'''" < 0.1
			UNION
			SELECT T2."ids" AS "Ensembl", -2 AS "status"
			FROM   table2 T2 LEFT JOIN table1 T1 ON T2."ids" = T1."Ensembl" 
			WHERE  T1."Ensembl" IS NULL AND T2."meanExpr'''+ID+'''" < 10
            ''',
		@name    = "statusTable_"+ID
	)
	
    network = InteractionNetwork
    (
    	force genes  = degCalledLst[ID],
		force status = statusTable,
		moksiskaan   = moksiskaanInit.connection,
		ensembl      = ensemblDb,
		organism     = Organism_Homo_sapiens,
		title        = "Interaction network of DEGs between " + group.caseG + " and " + group.controlG,
		linkTypes    = std.concat(sep=",",
			//LinkType_pathway_precedence,
			//LinkType_protein_protein_interaction,
			LinkType_chemical_reaction,
			LinkType_protein_activation,
			LinkType_protein_inhibition,
			LinkType_protein_state_change,
			LinkType_protein_binding,
			LinkType_protein_dissociation,
			LinkType_gene_expression,
			LinkType_gene_repression,
			LinkType_phosphorylation,
			LinkType_dephosphorylation,
			LinkType_glycosylation,
			LinkType_ubiquitination,
			LinkType_deubiquitination,
			LinkType_methylation,
			LinkType_demethylation,
			LinkType_acetylation,
			LinkType_deacetylation,
			LinkType_sumoylation,
			LinkType_desumoylation
		),
		annotRules        = "",
		bioentityTypes    = BioentityType_gene,
		maxGap            = 1,
		cytoscape         = false,
		useStudies        = "",
		hideGaps          = false,
		isolateGroupNames = false,
		expand            = "connected",
		statusFilter      = "NA",
		sectionType       = "subsubsection",
		@name             = "network_"+ID,
		@enabled          = useFunct
	)
 
	//--- COMBINE REPORTS -----------------------------------------------------------------------------//

	degReportLst[ID] = LatexCombiner
	(
		array = {
			degTableUp, 
			degBoxplotUp,
			degTableDn, 
			degBoxplotDn, 
			exprHeatmap, 
			goEnrichment,
			network.report
		},
		sectionTitle=ID, 
		sectionType="subsection"
	)
}

degReport = LatexCombiner
(
	array        = degReportLst,
	pagebreak    = true,
	tail         = '\newpage{}',
	sectionTitle = "Differentially expressed genes"
)

//------------------------------------------------------------------------------------------------
//--- GENE SET ENRICHMENT ANALYSIS (GSEA)
//------------------------------------------------------------------------------------------------
include "gsea.and"

gseaMSigDBUp = record()
gseaMSigDBDn = record()
gseaGeneSigDBUp = record()
gseaGeneSigDBDn = record()

for ID, group : groups 
{	
	rnk = DEG2Rnk
	(
		deg = degAllLst[ID],
		annotation = geneNames,
		colP = "p" + ID,
		colFC= "fc" + ID,
		@name = "rnk_" + ID
	)

	// run GSEA
	//---
	gseaMSigDB = GSEA
	(
		rnk = rnk,
		categories = genesetCategories,
		label = ID,
		gmt   = "/mnt/projects/generic/data/msigdb5.0/msigdb.v5.0.symbols.gmt,/mnt/projects/iamp/results/gsea/custom/mir_target_genesets.gmx",
		options = "-nperm 100 -set_max 500 -set_min 10",
		@name = "gseaMSigDB_" + ID,
		@enabled = true
	)
	gseaMSigDBUp[ID] = gseaMSigDB.enrichedUp
	gseaMSigDBDn[ID] = gseaMSigDB.enrichedDown

	gseaDSigDB = GSEA
	(
		rnk = rnk,
		categories = genesetCategories,
		label = ID,
		gmt   = "/mnt/projects/generic/data/DSigDB/DSigDB_v1.0_All.nodup.gmt",
		options = "-nperm 100 -set_max 1000 -set_min 5",
		@name = "gseaDSigDB_" + ID,
		@enabled = false
	)

	gseaGeneSigDB = GSEA
	(
		rnk = rnk,
		categories = genesetCategories,
		label = ID,
		gmt   = "/mnt/projects/generic/data/GeneSigDB/ALL_SIGSv4.nodup.gmt",
		options = "-nperm 100 -set_max 1000 -set_min 5",
		@name = "gseaGeneSigDB_" + ID,
		@enabled = false
	)
	gseaGeneSigDBUp[ID] = gseaGeneSigDB.enrichedUp
	gseaGeneSigDBDn[ID] = gseaGeneSigDB.enrichedDown
	
}

gseaReportMSigDB = getGSEAReportMSigDB
(
	enrichedUp = gseaMSigDBUp,
	enrichedDn = gseaMSigDBDn,
	groups=groups 
)

//------------------------------------------------------------------------------------------------
//--- DEG REPORTS OF miRNA TARGETS GENES
//------------------------------------------------------------------------------------------------

mirTargets = INPUT(path="/mnt/projects/iamp/data/miRecords/miRecords.predictedTargets.txt")
miRs = TableQuery(table1 = mirTargets, query = '''SELECT DISTINCT "miRNA ID" FROM table1''')

degReportMirLstGrp = record()
for ID, group : groups  
{
	if (ID=="iAMPvsDS" || ID=="iAMPvsER" || ID=="ERvsDS" || ID=="iAMPvsNoniAMP") 
	{	
		degReportMirLst = record()
		for miR: std.split("sa-miR-155 hsa-miR-125b hsa-miR-125a-3p")
		{ 
			instanceSuffix = ID + "_" + std.quote(miR, type="anduril")
		
			degTableMir = TableQuery
			(
				table1 = degTables[ID] @require,
				table2 = mirTargets @require,
				query  = """
					SELECT * FROM table1 WHERE "Gene" IN (SELECT DISTINCT "Symbol" FROM table2 A WHERE A."miRNA ID" = '""" + miR + """')
		            """,
				@name  = "degTableMir_" + instanceSuffix
			)
			
			degTableMirUp = getDEGTableReport
			(
				degs = degTableMir,
				degReportRefs = ensemblRef,
				direction = "up",
				group = group,
				ID=ID,
				@name  = "degTableMirUp_" + instanceSuffix
			)
		
			degTableMirDn = getDEGTableReport
			(
				degs = degTableMir,
				degReportRefs = ensemblRef,
				direction = "down",
				group = group,
				ID=ID,
				@name  = "degTableMirDn_" + instanceSuffix
			)
		
			degReportMirLst[instanceSuffix] = LatexCombiner
			(
				latex1=degTableMirUp, 
				latex2=degTableMirDn, 
				sectionTitle = miR + " target genes differentially expressed comparing " + ID, 
				sectionType = "subsubsection"
			)
		}
		
		degReportMirLstGrp[ID] = LatexCombiner
		(
			array=degReportMirLst, 
			sectionTitle = ID, 
			sectionType = "subsection"
		)
		
	}
}

degReportMir = LatexCombiner
(
	array        = degReportMirLstGrp, 
	pagebreak    = true,
	tail         = '\newpage{}',
	sectionTitle = "Differentially expressed target genes of selected microRNAs", 
	sectionType  = "section"
)


//------------------------------------------------------------------------------------------------
//--- EXPRESSION BOX PLOTS OF SELECTED GENES
//------------------------------------------------------------------------------------------------

genesCRA = TableQuery(table1 = geneAnnot @require,  query  = '''SELECT "Ensembl Gene ID" AS "Ensembl" FROM table1 WHERE "Chromosome Name" = '21' AND "Gene Start (bp)" >= 33192000 AND "Gene End (bp)" <= 39796000 ORDER BY "Gene Start (bp)"''')
boxplotCRA = ExpressionBoxPlot
(
	expr   = deseqExprMatrix.expr    @require @doc="Expression matrix",
	groups = sampleGroupsSimple  @require @doc="Sample sets",
	groupOrder = "CD19,ER,DS,iAMP",
	annotation = geneAnnot           @require @doc="Gene annotations",
	geneIds = genesCRA,
	nCol = 10,
	nRow = 12,
	height=9,
	caption = 'Expression of genes located on chromosome 21 in common region of amplification (CRA), as defined by Strefford et al. (2006). This region extends from 33,192,000 bp to 39,796,000 bp.',
	sectionTitle="Common region of amplification (CRA)", 
	sectionType="subsection"
)

boxplotRibosomalProteins = ExpressionBoxPlot
(
	expr   = deseqExprMatrix.expr,
	groups = sampleGroupsSimple,
	groupOrder = "CD19,ER,DS,iAMP",
	annotation = geneAnnot,
	hgnc = "RPL10,RPL10A,RPL10L,RPL11,RPL12,RPL13,RPL13A,RPL14,RPL15,RPL17,RPL18,RPL18A,RPL19,RPL21,RPL22,RPL22L1,RPL23,RPL23A,RPL24,RPL26,RPL26L1,RPL27,RPL27A,RPL28,RPL29,RPL3,RPL30,RPL31,RPL32,RPL34,RPL35,RPL35A,RPL36,RPL36A,RPL36AL,RPL37,RPL37A,RPL38,RPL39,RPL39L,RPL3L,RPL4,RPL41,RPL5,RPL6,RPL7,RPL7A,RPL7L1,RPL8,RPL9,RPLP0,RPLP1,RPLP2,RPS10,RPS11,RPS12,RPS13,RPS14,RPS15,RPS15A,RPS16,RPS17,RPS17L,RPS18,RPS19,RPS19BP1,RPS2,RPS20,RPS21,RPS23,RPS24,RPS25,RPS26,RPS27,RPS27A,RPS27L,RPS28,RPS29,RPS3,RPS3A,RPS4X,RPS4Y1,RPS4Y2,RPS5,RPS6,RPS6KA1,RPS6KA2,RPS6KA3,RPS6KA4,RPS6KA5,RPS6KA6,RPS6KB1,RPS6KB2,RPS6KC1,RPS6KL1,RPS7,RPS8,RPS9,RPSA",
	nRow = 7,
	nCol = 10,
	height=9,
	caption = 'Expression of genes coding for ribosomal proteins.',
	sectionTitle="Ribosomal proteins", 
	sectionType="subsection"
)

boxplotEpigeneticModifiers = ExpressionBoxPlot
(
	expr   = deseqExprMatrix.expr,
	groups = sampleGroupsSimple,
	groupOrder = "CD19,ER,DS,iAMP",
	annotation = geneAnnot,
	hgnc = "HDAC1,HDAC2,HDAC3,HDAC4,HDAC5,HDAC6,HDAC7,HDAC8,HDAC9,HDAC10,HDAC11,EP300,EZH1,EZH2,SUZ12,EED,EHMT1,EHMT2,KMT2A,KMT2D,KMT2C,DNMT1,DNMT3A,DNMT3B,TET1,TET2,CREBBP,SUV39H1",
	nRow = 4,
	nCol = 7,
	height=9,
	caption = 'Expression of epigenetic modifier genes. EHMT2=G9A; KMT2A=MLL1; KMT2D=MLL2; KMT2C=MLL3.',
	sectionTitle="Epigenetic modifiers", 
	sectionType="subsection"
)

boxplotOther = ExpressionBoxPlot
(
	expr   = deseqExprMatrix.expr    @require @doc="Expression matrix",
	groups = sampleGroupsSimple  @require @doc="Sample sets",
	groupOrder = "CD19,ER,DS,iAMP",
	annotation = geneAnnot           @require @doc="Gene annotations",
	hgnc = "ETV6,RUNX1,CRLF2,P2RY8,EBF1,PAX5,VPREB1,HMGN1,MME,CD34,CD19,MS4A1,SPI1,ETS2,GATA2,GAPDH,KIT,ERG,LGMN,TEK,CD40,TRAF1,TRAF2,TRAF3,TRAF5,MLLT6,MYC,GUSB",
	nCol = 6,
	nRow = 5,
	caption = 'Expression of other selected genes of interest. MME=CD10; MS4A1=CD20; KIT=c-Kit; TEK=Tie-2; PU.1=SPI1',
	sectionTitle="Other genes of interest", 
	sectionType="subsection"
)


boxplotSelectedGenes = LatexCombiner
(
	array        = {boxplotCRA.document, boxplotRibosomalProteins.document, boxplotEpigeneticModifiers.document, boxplotOther.document}, 
	pagebreak    = true,
	sectionTitle = "Expression values of selected genes", 
	sectionType  = "section"
)

//------------------------------------------------------------------------------------------------
//--- POSITIONAL GENE ENRICHMENT
//------------------------------------------------------------------------------------------------

include "pgeReport.and"

pgeReportLst = record()
for comparison : std.itercsv(comparisons) {
	
	pgeReportLst[comparison.ID] = PGEReport
	(
		query=degCalledLst[comparison.ID], 
		reference=gtf, 
		title=comparison.ID,
		pvalue=0.01,
		includeHeader=std.length(pgeReportLst)==0,
		@name="pge_"+comparison.ID
	)
}
	
pgeReport = LatexCombiner
(
	array=pgeReportLst, 
	pagebreak=true, 
	sectionTitle="Positional gene enrichment (PGE) of differentially expressed genes", 
	sectionType="section"
)

//------------------------------------------------------------------------------------------------
//--- FUNCTIONAL ANALYSIS REPORT
//------------------------------------------------------------------------------------------------

include "functionalAnalysis.and"
funcReport = createFunctionalAnalysisReport
(
	geneGO=geneGO,
	groups=groups, 
	degTables=degTables, 
	gstats=degAllLst, 
	degLst=degCalledLst,
	@enabled = useFunct
)

//------------------------------------------------------------------------------------------------
//--- FINAL REPORTS
//------------------------------------------------------------------------------------------------

rConfig = RConfigurationReport(packages = "base,csbl.go,DESeq2,igraph", sectionType = "section")
bibtexMoksiskaan = INPUT(path="/usr/local/share/anduril-bundles/moksiskaan/components/report-BibTeX/moksiskaan.bib")
bibtexSequencing = INPUT(path="/usr/local/share/anduril-bundles/sequencing/components/report-BibTeX/sequencing.bib")

docTemplate = LatexTemplate
(
	authors  = std.quote(authors, type="LaTeX"),
	bibtex1  = rConfig.citations,
	bibtex2  = bibtexMoksiskaan,
	bibtex3  = bibtexSequencing,
	title    = std.quote(title, type="LaTeX"),
	printTOC = true
)

combinedReport = LatexCombiner
(
	array={
		experimentReport,
		qcReport,
		degReport,
		boxplotSelectedGenes,
		pgeReport,
		degReportMir,
		gseaReportMSigDB,
		rConfig.report
	}
)

combinedPDF = LatexPDF
(
	document = combinedReport,
	header   = latexHeader,
	footer   = docTemplate.footer,
	useRefs  = true,
	verbose  = false
)

functioPDF = LatexPDF
(
	document = funcReport,
	header   = latexHeader,
	footer   = docTemplate.footer,
	useRefs  = true,
	verbose  = false,
	@enabled = useFunct
)
	

OUTPUT(combinedPDF.document)
OUTPUT(functioPDF.document)

//------------------------------------------------------------------------------------------------
//--- EXCEL FILES WITH DIFFERENTIALLY EXPRESSED GENES
//------------------------------------------------------------------------------------------------

@out.excelFile.filename = "diff-expressed-genes.xls"
degCalledExcel = CSV2Excel
(
	array         = degTables,
	refs          = ensemblRef,
	frozenColumns = 2,
	frozenRows    = 1,
	missingValue  = ''
)

OUTPUT(degCalledExcel.excelFile)

/*
eCSSRow = ""
for ID, group : groups
{ 
	eCSSRow = "*\tfc"+ID+"\t0.00\ttrue\tNA\tNA\tNA\t#0000BB\t#ffffff\tNA\t<= -"+fcLimitInclude+"\n"+
            "*\tfc"+ID+"\t0.00\ttrue\tNA\tNA\tNA\t#00FF00\t#ffffff\tNA\t>= "+ fcLimitInclude+"\n"+
            "*\tp"+ID+"\t0.00e00\tNA\tNA\tNA\tNA\tNA\tNA\tNA\tNA\n"+
            "*\tq"+ID+"\t0.00e00\tNA\tNA\tNA\tNA\tNA\tNA\tNA\tNA\n"+
            "*\tp"+ID+"\tNA\ttrue\tNA\tNA\tNA\t#ff0000\tNA\tNA\t<= "+pLimitInclude+"\n"+
            "*\tq"+ID+"\tNA\ttrue\tNA\tNA\tNA\t#ff0000\tNA\tNA\t<= "+qLimitInclude+"\n"+
            eCSSRow
}

degAllTable = CSVJoin(array = degAllLst @require, intersection = true)

degAllCSS = StringInput
(
	content=std.concat(sep="\n",
		"Row\tColumn\tFormat\tBold\tAlign\tFontSize\tUnderline\tTextColor\tBGColor\tBorder\tCondition",
		"1\t*\tNA\ttrue\tNA\t12\ttrue\t#000000\t#ffffff\tNA\tNA",
		"*\tEnsembl\tNA\tfalse\tleft\t6\tfalse\tNA\tNA\tNA\tNA",
		"*\tDNARegion\tNA\tNA\tleft\t6\tfalse\tNA\tNA\tNA\tNA",
		eCSSRow
	)
)

@out.excelFile.filename = "all-genes.xls"
degAllExcel = CSV2Excel
(
	csv           = degAllTable,
	refs          = ensemblRef,
	style         = degAllCSS,
	frozenColumns = 2,
	frozenRows    = 1,
	sheetNames    = "statistics"
)

OUTPUT(degAllExcel.excelFile)
*/

